/*=======================================================
-- Author:         alejandro.ochoa
-- Create date:    11-05-2018
-- Description:    Inserta los clientes en DB Intermedia para SPC
				   

-- EJEMPLO DE EJECUCION: 
	EXEC [diprocom].[SWIFT_SP_POST_CUSTOMER_NEW_TO_ERP]

=========================================================*/
CREATE PROCEDURE [DIPROCOM].[SWIFT_SP_POST_CUSTOMER_NEW_TO_ERP]
AS
BEGIN

	SET NOCOUNT ON

	DECLARE 
		@CUSTOMER INT
		,@CODE_CUSTOMER VARCHAR(50)
		,@ERROR_MSG VARCHAR(MAX)
		,@GIRO VARCHAR(30)
		,@CATEGORY VARCHAR(1)

	SELECT 
		[CUSTOMER]
		,[CODE_CUSTOMER]
	INTO #NEW_CUSTOMERS
	FROM [diprocom].[SWIFT_VIEW_ALL_CUSTOMER_NEW]
	WHERE [CODE_ROUTE] NOT IN ('4041','4042')
	AND ISNULL(IS_POSTED_ERP, -1) <> 1
		AND ISNULL(IS_SENDING, 0) = 0
		AND [IS_READY_TO_SEND] = 1
		--AND [CODE_CUSTOMER_BO] IS NULL
		AND GPS <> '0,0'
		AND ISNULL([ATTEMPTED_WITH_ERROR],0) <= 1  
		--AND [MUNICIPALITY] <> 'NO ESPECIFICADO'
		--AND [DEPARTAMENT] <> 'NO ESPECIFICADO'
		--AND spih.[POSTED_DATETIME] >= FORMAT(GETDATE(),'yyyyMMdd')
	ORDER BY [CUSTOMER] ASC
	
	WHILE EXISTS (SELECT TOP 1 1 FROM #NEW_CUSTOMERS)
	BEGIN
		
		SELECT TOP 1 
			@CUSTOMER = [CUSTOMER]
			,@CODE_CUSTOMER = [CODE_CUSTOMER]
		FROM #NEW_CUSTOMERS
		ORDER BY [CUSTOMER] ASC

		SELECT TOP 1 
			@GIRO = SUBSTRING([tag].[TAG_COMMENTS],1,29)
		FROM [DIPROCOM].[SWIFT_VIEW_ALL_CUSTOMER_NEW] [vacn]
		INNER JOIN [DIPROCOM].[SWIFT_VIEW_ALL_TAG_X_CUSTOMER_NEW] [tcn] ON [tcn].[CUSTOMER] = [vacn].[CODE_CUSTOMER]
		INNER JOIN [DIPROCOM].[SWIFT_TAGS] [tag] ON [tag].[TAG_COLOR] = [tcn].[TAG_COLOR] 
			AND [tag].[TYPE]='CUSTOMER' AND [tag].[TAG_VALUE_TEXT] LIKE 'GIRO_%'
		WHERE [vacn].[CUSTOMER] = @CUSTOMER
		ORDER BY [tag].[TAG_PRIORITY] ASC

		SELECT TOP 1 
			@CATEGORY = SUBSTRING([tag].[TAG_COMMENTS],1,1)
		FROM [DIPROCOM].[SWIFT_VIEW_ALL_CUSTOMER_NEW] [vacn]
		INNER JOIN [DIPROCOM].[SWIFT_VIEW_ALL_TAG_X_CUSTOMER_NEW] [tcn] ON [tcn].[CUSTOMER] = [vacn].[CODE_CUSTOMER]
		INNER JOIN [DIPROCOM].[SWIFT_TAGS] [tag] ON [tag].[TAG_COLOR] = [tcn].[TAG_COLOR] 
			AND [tag].[TYPE]='CUSTOMER' AND [tag].[TAG_VALUE_TEXT] LIKE 'CAT_%'
		WHERE [vacn].[CUSTOMER] = @CUSTOMER
		ORDER BY [tag].[TAG_PRIORITY] ASC

		BEGIN TRY

			INSERT INTO [DIPROCOM_SERVER].[SONDA].[dbo].[REPLICA_CLIENTE]
					( [CODIGO_SONDA] ,
						[CATEGORIA_CLIENTE] ,
						[NOMBRE_CLIENTE] ,
						[NOMBRE_COMERCIAL] ,
						[CONTACTO_CLIENTE] ,
						[DIRECCION_CLIENTE] ,
						[TELEFONO_CLIENTE] ,
						[NIT_CLIENTE] ,
						[CEDULA_CLIENTE] ,
						[GIRO_DE_NEGOCIO] ,
						[CODIGO_DE_PAIS] ,
						[CODIGO_DEPARTAMENTO] ,
						[CODIGO_MUNICIPIO] ,
						[CODIGO_TERRITORIO] ,
						[ESTADO_REPLICA] ,
						[FECHA_REPLICA] ,
						[LATITUD] ,
						[LONGITUD]
					)
			SELECT
				[vacn].[CODE_CUSTOMER]
				,ISNULL(@CATEGORY,(SELECT TOP 1 [TAG_COMMENTS] FROM [DIPROCOM].[SWIFT_TAGS] 
									WHERE [TYPE]='CUSTOMER' AND [TAG_VALUE_TEXT] LIKE 'CAT_%' 
									ORDER BY [TAG_PRIORITY] ASC	))
				,[vacn].[NAME_CUSTOMER]
				,[vacn].[INVOICE_NAME]
				,[vacn].[CONTACT_CUSTOMER]
				,SUBSTRING([vacn].[INVOICE_ADDRESS], 1, 60) 
				,[vacn].[PHONE_CUSTOMER]
				,[vacn].[NIT]
				,SUBSTRING([vacn].[ADRESS_CUSTOMER], 1, 60) 
				,ISNULL(@GIRO,(SELECT TOP 1 [TAG_COMMENTS] FROM [DIPROCOM].[SWIFT_TAGS] 
									WHERE [TYPE]='CUSTOMER' AND [TAG_VALUE_TEXT] LIKE 'GIRO_%' 
									ORDER BY [TAG_PRIORITY] ASC	))
				,1
				,5
				,1
				,[us].[RELATED_SELLER]
				,'P'
				,NULL
				,[vacn].[LATITUDE]
				,[vacn].[LONGITUDE]
			FROM [diprocom].[SWIFT_VIEW_ALL_CUSTOMER_NEW] [vacn]
				INNER JOIN [DIPROCOM].[USERS] [us] ON [us].[LOGIN] = [vacn].[POSTED_BY]
			WHERE [vacn].[CUSTOMER] = @CUSTOMER
		
			EXEC [diprocom].[SWIFT_SP_STATUS_SEND_CUSTOMER_TO_SAP] 
				@CODE_CUSTOMER = @CODE_CUSTOMER,
				@POSTED_RESPONSE = 'Proceso Exitoso',
				@CODE_CUSTOMER_BO = @CODE_CUSTOMER

			DELETE FROM [#NEW_CUSTOMERS] WHERE [CODE_CUSTOMER]=@CODE_CUSTOMER

		END TRY
		BEGIN CATCH

			SELECT @ERROR_MSG = CONCAT(@@ERROR,'_',ERROR_MESSAGE())

			EXEC [diprocom].[SWIFT_SP_CUSTOMER_ERROR_TO_SEND_SAP] @CODE_CUSTOMER = @CODE_CUSTOMER , -- varchar(50)
				@POSTED_RESPONSE = @ERROR_MSG

			DELETE FROM [DIPROCOM_SERVER].[SONDA].[dbo].[REPLICA_CLIENTE]
			WHERE [CODIGO_SONDA] = @CODE_CUSTOMER AND [ESTADO_REPLICA] = 'P'

			DELETE FROM [#NEW_CUSTOMERS] WHERE [CODE_CUSTOMER]=@CODE_CUSTOMER
			
		END CATCH
	END
END











